
;////////////////////////////////////////////////////////////////////////////////////////////
;//
;// pmc_11_SewRightHead.pmc
;// Created on: 03/19/2013  Author: Josh Kelley
;// 
;// Handles Sewing Using the Right Sew Head
;//
;////////////////////////////////////////////////////////////////////////////////////////////


CLOSE
DELETE GATHER
;#include "..\Headers\Download.h"

OPEN PROG_11_SEW_RIGHT  CLEAR


	;//Calculate the X axis move to position
	pXAxisMoveToPosition =  ((pStartPosition_mm + (pLHeadStitchCnt * pLHeadStitchLength_mm) + pMiddleGap_mm ) - (NEEDLE_OFFSET_MM + pLHeadStitchLength_mm) );
	
	;//Move the X axis the middle Gap distance
	LINEAR;
	FRAX(X);
	ABS(X);
	TA(200);
	TS(75);
	X(pXAxisMoveToPosition) F(pXAxisFeedRate_mm_Sec *60_SEC);
	dwell(10);

	;//Index and Start the Sewing Operation
	INC;
	TM(250);
	B(30)C(30);


	;//Do The SlowStitches
	pStitchIdx = 0;
	dac3RhtThreadBrakeTension_cnts == pRHeadSlowStitchTension;
	while(pStitchIdx < pRHeadSlowStitchCount and pStitchIdx < pRHeadStitchCnt)

		if(pRHeadUseAltStartTension = TRUE or pRHeadSewLowStitchDensity = TRUE)

			PVT(pRHeadSlowStitchTimeSlice);
			X 0.00:0.00 B 146.155:4.151667 C 146.155:4.151667;
			X 0.00:0.00 B 146.155:4.151667 C 146.155:4.151667;
			X 0.00:0.00 B 67.69:3.723 C 67.69:3.723;
			dac3RhtThreadBrakeTension_cnts == pRHeadSlowStitchTension;
			X (pRHeadStitchLength_mm):(pRHeadStitchLength_mm / 30.0) B 0.00:0 C0.0:0;
			dac3RhtThreadBrakeTension_cnts == pRHeadFiberTension_cnts;

		else

			PVT(pRHeadSlowStitchTimeSlice);
			X 0.00:0.00 B 146.155:4.151667 C 146.155:4.151667;
			X 0.00:0.00 B 146.155:4.151667 C 146.155:4.151667;
			X (pRHeadStitchLength_mm):(pRHeadStitchLength_mm / 30.0) B 67.69:3.723 C 67.69:3.723;
		
		endif
		
		pStitchIdx = pStitchIdx + 1;
	endwhile

	;//Release the Right Thread Clamp
	oRightThreadClamp == 0;


	;//Calculate the Ramp Rate (Make Sure our Time Slice Is Faster than Our Slow Stitch Time Slice
	if( (pRHeadSlowStitchTimeSlice - pRHeadStitchTimeSlice) > 0)
		pStitchRampRate = ((pRHeadSlowStitchTimeSlice - pRHeadStitchTimeSlice) / pRHeadSlowStitchRampCount);
	else
		pStitchRampRate = 0.0;
	endif


	;//Sew The Remaining amout Of Stitches
	pRampStitchIdx = 0;
	while(pStitchIdx < pRHeadStitchCnt)
		
		if(pRampStitchIdx < pRHeadSlowStitchRampCount)
			PVT((pRHeadSlowStitchTimeSlice - ((pRampStitchIdx + 1) * pStitchRampRate)))
			pRampStitchIdx = pRampStitchIdx + 1;
		else
			PVT(pRHeadStitchTimeSlice)
		endif

		if(pRHeadUseAltStartTension = TRUE and pRampStitchIdx < pRHeadSlowStitchRampCount or pRHeadSewLowStitchDensity = TRUE)

			X 0.00:0.00 B 146.155:4.151667 C 146.155:4.151667;
			X 0.00:0.00 B 146.155:4.151667 C 146.155:4.151667;
			X 0.00:0.00 B 67.69:3.723 C 67.69:3.723;
			dac3RhtThreadBrakeTension_cnts == pRHeadSlowStitchTension;
			X (pRHeadStitchLength_mm):(pRHeadStitchLength_mm / 30.0) B 0.00:0 C0.0:0;
			dac3RhtThreadBrakeTension_cnts == pRHeadFiberTension_cnts;

		else

			if(pRampStitchIdx < pRHeadSlowStitchRampCount)
			
				dac3RhtThreadBrakeTension_cnts == pRHeadSlowStitchTension;
			else
				dac3RhtThreadBrakeTension_cnts == pRHeadFiberTension_cnts;
			endif

			X 0.00:0.00 B 146.155:4.151667 C 146.155:4.151667;
			X 0.00:0.00 B 146.155:4.151667 C 146.155:4.151667;
			X (pRHeadStitchLength_mm):(pRHeadStitchLength_mm / 30.0) B 67.69:3.723 C 67.69:3.723;

		endif
	
		pStitchIdx = pStitchIdx + 1;

	endwhile


	dwell(0);
	;//Kill the Thread Brake Tension
	while (dac3RhtThreadBrakeTension_cnts > 50)

		dac3RhtThreadBrakeTension_cnts = dac3RhtThreadBrakeTension_cnts - (dac3RhtThreadBrakeTension_cnts *0.30)
		dwell(150)
	endwhile

	dac3RhtThreadBrakeTension_cnts = 0;

	
	;//Reset the Needle Looper Orientation
	INC;
	TM(250);
	B(-30)C(-30);


	;//Set the Done Bit 
	mMachineStatus0 = mMachineStatus0 | SEW_RIGHT_HEAD_DONE;


CLOSE